// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '7.4.1' apply false
    id 'com.android.library' version '7.4.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.10' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.8.10' apply false
    id 'com.google.devtools.ksp' version "1.8.10-1.0.9" apply false
}

project(':common') {
    apply plugin: 'com.android.library'
    apply plugin: 'org.jetbrains.kotlin.android'
    apply plugin: 'maven-publish'

    afterEvaluate {
        publishing {
            repositories {
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/wzcxbg/common")
                    Properties properties = new Properties()
                    properties.load(project.rootProject.file('local.properties').newDataInputStream())
                    def uname = properties.getProperty('github.uname')
                    def token = properties.getProperty('github.token')
                    credentials {
                        username = uname
                        password = token
                    }
                }
            }

            publications {
                release(MavenPublication) {
                    from components.release
                    groupId = project.group
                    artifactId = project.name
                    version = project.version
                }
                /*debug(MavenPublication) {
                    from components.debug
                    groupId = project.group
                    artifactId = project.name
                    version = project.version
                }*/
            }
        }
    }
}

project(':config-android') {
    apply plugin: 'com.android.library'
    apply plugin: 'org.jetbrains.kotlin.android'
    apply plugin: 'maven-publish'

    afterEvaluate {
        publishing {
            publications {
                release(MavenPublication) {
                    from components.release
                    groupId = project.group
                    artifactId = project.name
                    version = project.version
                }
            }
        }
    }
}

project(':config-annotation') {
    apply plugin: 'java-library'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'maven-publish'

    java {
        withSourcesJar()
        withJavadocJar()
    }

    afterEvaluate {
        publishing {
            publications {
                release(MavenPublication) {
                    from components.java
                    groupId = project.group
                    artifactId = project.name
                    version = project.version
                }
            }
        }
    }
}

project(':config-ksp-compiler') {
    apply plugin: 'java-library'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'maven-publish'

    java {
        withSourcesJar()
        withJavadocJar()
    }

    afterEvaluate {
        publishing {
            publications {
                release(MavenPublication) {
                    from components.java
                    groupId = project.group
                    artifactId = project.name
                    version = project.version
                }
            }
        }
    }
}
