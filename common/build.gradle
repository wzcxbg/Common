plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'maven-publish'
    id 'com.google.devtools.ksp'
}

android {
    namespace 'com.sliver.common'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    kotlin {
        sourceSets {
            main.kotlin.srcDirs += 'build/generated/ksp'
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'
    implementation 'androidx.startup:startup-runtime:1.1.1'
    api project(':config-annotation')
    ksp project(':config-ksp-compiler')
}


afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/wzcxbg/common")
                Properties properties = new Properties()
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                def uname = properties.getProperty('github.uname')
                def token = properties.getProperty('github.token')
                credentials {
                    username = uname
                    password = token
                }
            }
        }

        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.sliver'
                artifactId = 'common'
                version = '1.0'
            }
            debug(MavenPublication) {
                from components.debug
                groupId = 'com.sliver'
                artifactId = 'common-debug'
                version = '1.0'
            }
        }
    }
}